resource "aws_vpc" "main" {
	cidr_block = "10.0.0.0/24"
	tags = {
		Name = local.vpc_name
	}
}

resource "aws_subnet" "public" {
	vpc_id = aws_vpc.main.id
	cidr_block = "10.0.0.0/25"
	map_public_ip_on_launch = true

	tags = {
		Name = local.public_subnet_name
	}
}

resource "aws_subnet" "private" {
	vpc_id = aws_vpc.main.id
	cidr_block = "10.0.0.128/25"
	
	tags = {
		Name = local.private_subnet_name
	}
}

resource "aws_internet_gateway" "igw" {
	vpc_id = aws_vpc.main.id
	
	tags = {
		Name = local.igw_name
	}
}

resource "aws_eip" "nat" {
	domain = "vpc"
	
	tags = {
		Name = local.nat_eip_name
	}
}

resource "aws_nat_gateway" "nat" {
	allocation_id = aws_eip.nat.id
	subnet_id = aws_subnet.public.id

	tags = {
		Name = local.nat_gw_name
	}
}

resource "aws_route_table" "public" {
	vpc_id = aws_vpc.main.id
	route {
		cidr_block = "0.0.0.0/0"
		gateway_id = aws_internet_gateway.igw.id
	}

	tags = {
		Name = local.public_rt_name
	}
}

resource "aws_route_table" "private" {
	vpc_id = aws_vpc.main.id
	route {
		cidr_block = "0.0.0.0/0"
		nat_gateway_id = aws_nat_gateway.nat.id
	}
	
	tags = {
		Name = local.private_rt_name 
	}
}

resource "aws_route_table_association" "public" {
	subnet_id = aws_subnet.public.id
	route_table_id = aws_route_table.public.id
}

resource "aws_route_table_association" "private" {
	subnet_id = aws_subnet.private.id
	route_table_id = aws_route_table.private.id
}
